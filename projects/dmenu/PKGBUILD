# Maintainer: alfunx <alphonse.mariya@hotmail.com>

_pkgname=dmenu
pkgname=${_pkgname}-git
pkgver=4.7.r6.g8895128
pkgrel=1
pkgdesc='A generic menu for X. (Including patches and colorschemes)'
arch=('i686' 'x86_64')
url='https://st.suckless.org/'
license=('MIT')
options=('zipman')
depends=('sh' 'libxinerama' 'libxft')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

_patches=('https://tools.suckless.org/dmenu/patches/dmenu-lineheight-4.7.diff'
          'dmenu-fuzzymatch-20180302-8895128.diff'
          'dmenu-config-20180215-8895128.diff')

source=("$pkgname::git+file:///home/amariya/projects/dmenu/dmenu"
        "${_patches[@]}")

sha512sums=('SKIP'
            '04d3d7c48ca27a3e939136f5c979eebe4c9e7935a63323576e577e1146c6a7e61f969ba43d06ef9e66c6f3c37907da36aa806ee77393f5f5c6907c3e50ebdd34'
            'a03635159388379319078478abb6ccba1eae0b71d947465d1e1c94c692a11cd3a8b5168ad0f846a0e2987304f3e83fb22e5efa20507f872410ac15eb24f3daf7'
            'ece754e4de001f2a698cd2ceb00bbc0ae9f035f6f6abccfa7e4b7c8418a95cc79843334139a38f42da0c23681c8566f0e12d3027ba12475c6bc4b50d770b6961')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  local file
  cd "$pkgname"

  for file in "${_patches[@]}"; do
    if [[ "$file" == *.h ]]; then
      cp "$srcdir/$file" .
    elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
      echo -e "\e[1mApplying patch: $(basename "$file")\e[0m"
      patch -Np1 <"$srcdir/$(basename "$file")"
    fi
  done
}

build() {
  cd "$pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
