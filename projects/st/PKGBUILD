# Maintainer: alfunx <alphonse.mariya@hotmail.com>

_pkgname=st
pkgname=${_pkgname}-git
pkgver=0.7.r41.g1f24bde
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X. (Including patches and colorschemes)'
arch=('i686' 'x86_64')
url='https://st.suckless.org/'
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

_patches=(
    "https://st.suckless.org/patches/vertcenter/st-vertcenter-20170601-5a10aca.diff"
    "https://st.suckless.org/patches/hidecursor/st-hidecursor-20170404-745c40f.diff"
    "https://st.suckless.org/patches/scrollback/st-scrollback-20170329-149c0d3.diff"
    "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-20170427-5a10aca.diff"
    "https://st.suckless.org/patches/clipboard/st-clipboard-20170925-b1338e9.diff"
    "https://st.suckless.org/patches/relativeborder/st-relativeborder-20171207-0ac685f.diff"
    "st-gruvbox-colorscheme-20180212-1f24bde.diff"
    "st-font-iosevka-custom-20180212-1f24bde.diff"
    "st-worddelimiters-20180215.diff"
)

source=(
    "$pkgname::git+file:///home/amariya/projects/st/st"
    "${_patches[@]}"
)

sha512sums=(
    'SKIP'
    '319d8d456011cc1c3cc3abb9c7a8ba5018f315d8fec69141b23595d275f979e3146a3eb83a07b995c77a2027b14d784e5aeb745a0d6159fd57cbabd77bcaff18'
    '35a533e66069439cbca9c1c0eee4d73eaca527b011f4a1fdc09d7130f8f44711410117fed1fc9ac0a8666217228992b2edfa70284859bcf2ec740b37d3fc7de7'
    'a090914549f5161b7cd37f2994a32a4d5e1711505f6feb686734412249de7f28d7cc72946e157095857b88127e39742c978d6e190645028c5bb15b204723358b'
    'ffd0594e88de6f5aab7aebccb9e0efdb36be8ce43aa25b1a5ff1432075d23ab8d4dd2c75e74f3f716362b8ff2548080d44493c6ffe5aeaa3d1a8a84cf53ec231'
    'b7a854c26ebcc480606289b3dc25c4a482ce02eb5138918f35152b0a139b71084be658a39e79461ceaeabdee924872bae9e7c01bdd37514bcdb4eea1d2c01e0a'
    '243da02f4dc074d41bf234cc35609297bbf671532faea2cdff1f6aad9d7260b348e1d6a25e112467fab47558e5a7de3e34abd6905eddbdc1cd4197e90268bcc4'
    'SKIP'
    'SKIP'
    'SKIP'
)

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  local file
  cd "$pkgname"

  # skip terminfo which conflicts with nsurses
  # sed -i '/tic /,+1d' Makefile
  sed -i '/tic /d' Makefile

  for file in "${_patches[@]}"; do
    if [[ "$file" == *.h ]]; then
      cp "$srcdir/$file" .
    elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
      echo -e "\e[1mApplying patch: $(basename "$file")\e[0m"
      patch -Np1 <"$srcdir/$(basename "$file")"
    fi
  done
}

build() {
  cd "$pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
